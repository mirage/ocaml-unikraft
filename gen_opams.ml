(* SPDX-License-Identifier: MIT
 * Copyright (c) 2025 Samuel Hym, Tarides <samuel@tarides.com>
 *)

(* OCaml script to generate all the *.opam files *)

let repository_layout = ref false
let url = ref None

let _ =
  let url_src = ref "" in
  let url_and_checksum =
    let seen = ref 0 in
    fun arg ->
      match !seen with
      | 0 ->
          url_src := arg;
          incr seen
      | 1 -> url := Some (!url_src, arg)
      | _ -> failwith ("Don't know what to do with argument: " ^ arg)
  in
  Arg.parse
    [
      ( "-r",
        Arg.Set repository_layout,
        "Use the standard opam-repository layout (packages/...)" );
    ]
    url_and_checksum "gen_opams [-r]"

let version_ocaml_unikraft = "1.0.0"
let version_unikraft = "0.18.0"
let archs = [ "arm64"; "x86_64" ]
let backends = [ ("firecracker", "Firecracker"); ("qemu", "QEMU") ]
let options = [ ("debug", "debugging", []) ]

(** How the architecture appears in the tool prefixes *)
let prefix_arch = function
  | "arm64" -> "aarch64"
  | "x86_64" as x -> x
  | x -> failwith ("Unsupported arch: " ^ x)

let mkdir_p chunks =
  List.fold_left
    (fun prefix chunk ->
      let dir = Filename.concat prefix chunk in
      (try Sys.mkdir dir 0o755
       with Sys_error msg -> assert (String.ends_with ~suffix:"exists" msg));
      dir)
    "" chunks

let with_package virt package_name version gen =
  let filename =
    if !repository_layout then
      Filename.concat
        (mkdir_p
           [
             "packages";
             package_name;
             Printf.sprintf "%s.%s" package_name version;
           ])
        "opam"
    else Printf.sprintf "%s.opam" package_name
  in
  Out_channel.with_open_bin filename (fun out ->
      if not !repository_layout then
        Printf.fprintf out
          "# This file is generated by gen_opams.ml, edit it instead\n";
      Printf.fprintf out {|opam-version: "2.0"|};
      if not !repository_layout then
        Printf.fprintf out {|
name: "%s"
version: "%s"|} package_name version;
      Printf.fprintf out
        {|
maintainer: "samuel@tarides.com"
homepage: "https://github.com/mirage/ocaml-unikraft/"
bug-reports: "https://github.com/mirage/ocaml-unikraft/issues"
tags: "org:mirage"|};
      gen out;
      (match (virt, !url) with
      | true, _ | _, None -> ()
      | false, Some (src, checksum) ->
          Printf.fprintf out
            {|url {
  src:
    "%s"
  checksum:
    "sha256=%s"
}
|}
            src checksum);
      if !repository_layout then
        Printf.fprintf out
          {|available: os = "linux"
x-maintenance-intent: ["(latest)"]
|})

let backend_package arch backend =
  let short_name, long_name = backend in
  let package_name =
    Printf.sprintf "ocaml-unikraft-backend-%s-%s" short_name arch
  in
  with_package false package_name version_unikraft (fun out ->
      Printf.fprintf out
        {|
synopsis: "%s/%s Unikraft backend for OCaml"
authors: ["Samuel Hym" "Unikraft contributors"]
license: ["MIT" "BSD-3-Clause" "GPL-2.0-only"]
depends: [
  "unikraft" {= version}
]
depopts: [|}
        long_name arch;
      List.iter
        (fun (opt, _, _) ->
          Printf.fprintf out "\n  \"ocaml-unikraft-option-%s\"" opt)
        options;
      Printf.fprintf out
        {|
]
depexts: [
  ["gcc-%s-linux-gnu"] {os-family = "debian" & arch != "%s"}|}
        (prefix_arch arch) arch;
      (match arch with
      | "arm64" ->
          Printf.fprintf out
            {|
  ["%s-linux-gnu-gcc"] {os-family = "arch" & arch != "%s"}|}
            (prefix_arch arch) arch
      | _ -> () (* No cross compiler to x86_64 packaged in aarch64 Arch *));
      Printf.fprintf out
        {|
]
build: [
  [
    make
    "-j%%{jobs}%%"
    "UNIKRAFT=%%{lib}%%/unikraft"
    "OCUKPLAT=%s"
    "OCUKARCH=%s"
    "OCUKEXTLIBS=musl"
    "OCUKCONFIGOPTS+=debug" {ocaml-unikraft-option-debug:installed}
    "%%{name}%%.install"
  ]
]
extra-source "lib-musl.tar.gz" {
  src:
    "https://github.com/unikraft/lib-musl/archive/refs/tags/RELEASE-0.18.0.tar.gz"
  checksum:
    "sha256=b51afee0227c0c8c419dd001fb6b6f57b529e5cadcd437afdd05e2e8667a1e2e"
}
extra-source "patches/lib-musl/main-tsd.patch" {
  src:
    "https://raw.githubusercontent.com/shym/lib-musl/refs/heads/patches/main-tsd.patch"
  checksum:
    "sha256=9b87cbf0743492e6949de61af6423b463031da8b14b799a775c34886cabffd28"
}
extra-source "patches/lib-musl/arm64.patch" {
  src:
    "https://raw.githubusercontent.com/shym/lib-musl/refs/heads/patches/arm64.patch"
  checksum:
    "sha256=d83043f534a8da4f0133f4fbde0d78bc3a5d996ca6f7fc91b42ccf2874515514"
}
extra-source "patches/lib-musl/exit.patch" {
  src:
    "https://raw.githubusercontent.com/shym/lib-musl/refs/heads/patches/exit.patch"
  checksum:
    "sha256=41e72b400071b146dde8ecd7be09a4a6187d2ed448ae200581de5405f84db1ee"
}
extra-source "musl-1.2.3.tar.gz" {
  src: "https://www.musl-libc.org/releases/musl-1.2.3.tar.gz"
  checksum:
    "sha256=7d5b0b6062521e4627e099e4c9dc8248d32a30285e959b7eecaa780cf8cfd4a4"
}
|}
        short_name arch)

let option_package option =
  let short_name, long_name, conflicts = option in
  let package_name = Printf.sprintf "ocaml-unikraft-option-%s" short_name in
  with_package true package_name version_unikraft (fun out ->
      Printf.fprintf out
        {|
synopsis:
  "Virtual package to enable %s in the Unikraft backends"
authors: "Samuel Hym"
license: "MIT"
|}
        long_name;
      match conflicts with
      | [] -> ()
      | _ ->
          Printf.fprintf out "conflict-class: [\n";
          List.iter (Printf.fprintf out "  \"ocaml-unikraft-%s\"\n") conflicts;
          Printf.fprintf out "]\n")

let toolchain_package arch =
  let package_name = Printf.sprintf "ocaml-unikraft-toolchain-%s" arch in
  with_package false package_name version_ocaml_unikraft (fun out ->
      Printf.fprintf out
        {|
synopsis:
  "C toolchain to build an OCaml cross compiler to the freestanding Unikraft %s backends"
description:
  "This package provides a C toolchain to build an OCaml cross compiler, suitable for linking with a Unikraft %s unikernel."
authors: "Samuel Hym"
license: "MIT"
depends: [
  "ocaml-unikraft-backend-qemu-%s" | "ocaml-unikraft-backend-firecracker-%s"
]
build: [
  [
    make
    "-j%%{jobs}%%"
    "LIB=%%{lib}%%"
    "SHARE=%%{share}%%"
    "OCUKARCH=%s"
    "%%{name}%%.install"
  ]
]
|}
        arch arch arch arch arch)

let compiler_package arch =
  let package_name = Printf.sprintf "ocaml-unikraft-%s" arch in
  with_package false package_name version_ocaml_unikraft (fun out ->
      Printf.fprintf out
        {|
synopsis: "OCaml cross compiler to the freestanding Unikraft %s backends"
description:
  "This package provides an OCaml cross compiler, suitable for linking with a Unikraft %s unikernel."
authors: "Samuel Hym"
license: ["MIT" "LGPL-2.1-or-later WITH OCaml-LGPL-linking-exception"]
depends: [
  "ocaml" {= "5.3.0"}
  "ocaml-unikraft-toolchain-%s"
  "ocamlfind"
  "ocaml-src" {build}
  "conf-git" {build}
]
build: [
  [
    make
    "-j%%{jobs}%%"
    "prefix=%%{prefix}%%"
    "BIN=%%{bin}%%"
    "LIB=%%{lib}%%"
    "SHARE=%%{share}%%"
    "OCUKARCH=%s"
    "%%{name}%%.install"
  ]
]
install: [
  [make "install-ocaml"]
]
|}
        arch arch arch arch)

let default_compiler_package arch =
  let package_name = Printf.sprintf "ocaml-unikraft-default-%s" arch in
  with_package false package_name version_ocaml_unikraft (fun out ->
      Printf.fprintf out
        {|
synopsis:
  "OCaml default cross compiler to the freestanding Unikraft %s backends"
description:
  "This package provides an OCaml cross compiler, suitable for linking with a Unikraft %s unikernel, as the default `unikraft` ocamlfind toolchain."
authors: "Samuel Hym"
license: "MIT"
depends: ["ocaml-unikraft-%s" "ocamlfind"]
conflict-class: "ocaml-unikraft-default"
build: [
  [make "prefix=%%{prefix}%%" "OCUKARCH=%s" "%%{name}%%.install"]
]
|}
        arch arch arch arch)

let default_backend_package backend =
  let short_name, long_name = backend in
  let package_name = Printf.sprintf "ocaml-unikraft-backend-%s" short_name in
  with_package true package_name version_ocaml_unikraft (fun out ->
      Printf.fprintf out
        {|
synopsis:
  "Virtual package to ensure the %s Unikraft backend is installed for the default cross compiler"
description:
  "This virtual package ensures that the %s backend is installed for the default `unikraft` ocamlfind cross toolchain."
authors: "Samuel Hym"
license: "MIT"
depends: [
  "ocaml-unikraft"
  ("ocaml-unikraft-default-x86_64" & "ocaml-unikraft-backend-%s-x86_64") |
  ("ocaml-unikraft-default-arm64" & "ocaml-unikraft-backend-%s-arm64")
]
|}
        long_name long_name short_name short_name)

let main_package () =
  with_package true "ocaml-unikraft" version_ocaml_unikraft (fun out ->
      Printf.fprintf out
        {|
synopsis:
  "Virtual package to install one of the OCaml default cross compilers to the freestanding Unikraft backends"
description:
  "This virtual package ensures that an OCaml cross compiler is available for linking with a Unikraft unikernel as the default `unikraft` ocamlfind toolchain. Explicitly choose one among the ocaml-unikraft-default-* packages to control which one is actually installed."
authors: "Samuel Hym"
license: "MIT"
depends: ["ocaml-unikraft-default-x86_64" | "ocaml-unikraft-default-arm64"]
|})

let _ =
  List.iter (fun arch -> List.iter (backend_package arch) backends) archs;
  List.iter option_package options;
  List.iter toolchain_package archs;
  List.iter compiler_package archs;
  List.iter default_compiler_package archs;
  List.iter default_backend_package backends;
  main_package ()
